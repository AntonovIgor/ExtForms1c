
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД

Процедура ДействияФормыСформироватьОтчет(Кнопка)
	
	МассивИменРолей = Новый Массив;
	
	Если ТипПолномочия = "Роль" Тогда
		
		Если НЕ ЗначениеЗаполнено(РольРеквизит) Тогда
			Предупреждение(НСтр("ru = 'Не выбрана роль!'"), 60, "Ошибка!");
			Возврат;
		КонецЕсли;
		
		МассивИменРолей.Добавить(РольРеквизит);
		
	ИначеЕсли ТипПолномочия = "Роли" Тогда
		
		Для Каждого ЭлементСписка Из СписокРолейРеквизит Цикл
			Если ЭлементСписка.Пометка Тогда
				МассивИменРолей.Добавить(ЭлементСписка.Значение);
			КонецЕсли;
		КонецЦикла;
		
		Если МассивИменРолей.Количество() = 0 Тогда
			Предупреждение(НСтр("ru = 'Не выбрано ни одной роли!'"), 60, "Ошибка!");
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ТипПолномочия = "Пользователь" Тогда
		
		Если НЕ ЗначениеЗаполнено(ПользовательИБ) Тогда
			Предупреждение(НСтр("ru = 'Не выбран пользователь!'"), 60, "Ошибка!");
			Возврат;
		КонецЕсли;
		
		метаПользователь = ПолучитьПользователяИБПоИмени(ПользовательИБ);
		Для Каждого ТекРоль Из метаПользователь.Роли Цикл
			МассивИменРолей.Добавить(ТекРоль.Имя);
		КонецЦикла;
		
	КонецЕсли;
	
	ТабДок = СформироватьМакетТаблицыОтчета(МассивИменРолей, НеВыводитьОбъектБезПрав);
	
	ПолеОтчета = ЭлементыФормы.ПолеОтчета;
	ПолеОтчета.Очистить();
	ПолеОтчета.Вывести(ТабДок);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ОбновитьОтображениеГиперссылки()
	
	КолРолей = 0;
	СтрРоли = "";
	Для Каждого ЭлементСписка Из СписокРолейРеквизит Цикл
		Если ЭлементСписка.Пометка Тогда
			СтрРоли = СтрРоли + ", " + ЭлементСписка.Значение;
			КолРолей = КолРолей + 1;
		КонецЕсли;
	КонецЦикла;
	СтрРоли = Сред(СтрРоли,3);
	
	Если КолРолей > 0 Тогда
		ЭлементыФормы.ГиперрссылкаВыборРолей.Заголовок = "("+КолРолей+") "+?(СтрДлина(СтрРоли)>90, Лев(СтрРоли, 87)+"...", СтрРоли);
	Иначе
		ЭлементыФормы.ГиперрссылкаВыборРолей.Заголовок = "Не выбрано ни одной роли";
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриИзмененииТипаПолномочия()
	
	ЭлементыФормы.ПанельНастроек.ТекущаяСтраница = ЭлементыФормы.ПанельНастроек.Страницы[ТипПолномочия];
	
	ОбновитьОтображениеГиперссылки();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ И ЭЛЕМЕНТОВ ДИАЛОГА

Процедура ТипПолномочияПриИзменении(Элемент)
	
	ПриИзмененииТипаПолномочия();
	
КонецПроцедуры

Процедура ГиперрссылкаВыборРолейНажатие(Элемент)
	
	Если СписокРолейРеквизит.ОтметитьЭлементы("Выбор ролей для отчета") Тогда
		ОбновитьОтображениеГиперссылки();
	КонецЕсли
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	СписокРолей = ПолучитьСписокРолей();
	СписокПользователей = ПолучитьСписокПользователейИБ();
	
	ЭлементыФормы.РольРеквизит.СписокВыбора.Очистить();
	СписокРолейРеквизит.Очистить();
	Для Каждого ЭлементСписка Из СписокРолей Цикл
		ЭлементыФормы.РольРеквизит.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		СписокРолейРеквизит.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
	ЭлементыФормы.ПользовательИБ.СписокВыбора.Очистить();
	Для Каждого ЭлементСписка Из СписокПользователей Цикл
		ЭлементыФормы.ПользовательИБ.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
	ТипПолномочия = "Роль";
	ПриИзмененииТипаПолномочия();
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ПроверитьВерсиюПлатформыПриЗапуске(Отказ);
	
КонецПроцедуры
